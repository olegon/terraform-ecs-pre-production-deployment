data "aws_iam_policy_document" "codedeploy_trust" {
  statement {
    effect = "Allow"

    principals {
      type        = "Service"
      identifiers = ["codedeploy.amazonaws.com"]
    }

    actions = ["sts:AssumeRole"]
  }
}

resource "aws_iam_role" "codedeploy" {
  name               = format("%s-codedeploy-role", var.app_name)
  assume_role_policy = data.aws_iam_policy_document.codedeploy_trust.json
}

resource "aws_iam_role_policy_attachment" "aws_codedeploy_role_for_ecs" {
  policy_arn = "arn:aws:iam::aws:policy/AWSCodeDeployRoleForECS"
  role       = aws_iam_role.codedeploy.name
}

resource "aws_codedeploy_app" "app" {
  compute_platform = "ECS"
  name             = format("%s-codedeploy", var.app_name)
}

resource "aws_codedeploy_deployment_group" "app" {
  # It will throw an error if Rolling Update Deployment is active and a I try to create it
  # because this configurion only works with Blue Green. This is configured on aws_ecs_service.deployment_controller
  count = var.deployment_type == "Blue Green" ? 1 : 0

  app_name               = aws_codedeploy_app.app.name
  deployment_config_name = var.deployment_bluegreen_strategy
  deployment_group_name  = format("%s-codedeploy", var.app_name)
  service_role_arn       = aws_iam_role.codedeploy.arn

  auto_rollback_configuration {
    enabled = true
    events  = ["DEPLOYMENT_FAILURE"]
  }

  blue_green_deployment_config {
    deployment_ready_option {
      action_on_timeout = "CONTINUE_DEPLOYMENT"
    }

    terminate_blue_instances_on_deployment_success {
      action                           = "TERMINATE"
      termination_wait_time_in_minutes = 5
    }
  }

  deployment_style {
    deployment_option = "WITH_TRAFFIC_CONTROL"
    deployment_type   = "BLUE_GREEN"
  }

  ecs_service {
    cluster_name = var.ecs_cluster_name
    service_name = aws_ecs_service.app.name
  }

  load_balancer_info {
    target_group_pair_info {
      prod_traffic_route {
        listener_arns = [local.is_alb_attachment ? data.aws_lb_listener.lb_ingress_http_prod[0].arn : aws_lb_listener.nlb_pre_production_http_prod[0].arn]
      }

      test_traffic_route {
        listener_arns = [local.is_alb_attachment ? data.aws_lb_listener.lb_ingress_http_test[0].arn : aws_lb_listener.nlb_pre_production_http_test[0].arn]
      }

      target_group {
        name = local.is_alb_attachment ? aws_lb_target_group.alb_ingress_app_blue[0].name : aws_lb_target_group.nlb_ingress_app_blue[0].name
      }

      target_group {
        name = local.is_alb_attachment ? aws_lb_target_group.alb_ingress_app_green[0].name : aws_lb_target_group.nlb_ingress_app_green[0].name
      }
    }
  }
}
